#-------------------------------------------------
#
# Project created by QtCreator 2014-03-12T21:47:50
#
#-------------------------------------------------

QT       += core gui network xml multimedia

greaterThan(QT_MAJOR_VERSION, 4): QT += widgets

TARGET = RabbitIm
TEMPLATE = app

# Determine library name
CONFIG(debug, debug|release) {
    QXMPP_LIBRARY_NAME = qxmpp_d
} else {
    QXMPP_LIBRARY_NAME = qxmpp
}

#androidÈÄâÈ°π‰∏≠ÂåÖÂê´‰∫ÜunixÈÄâÈ°πÔºåÊâÄ‰ª•Âú®ÂÜôÂ∑•Á®ãÂ¶Ç‰∏ãÊù°‰ª∂Âà§Êñ≠Êó∂ÔºåÂøÖÈ°ªÊääandroidÊù°‰ª∂ÊîæÂú®unixÊù°‰ª∂Ââ?
android {
    INCLUDEPATH += $$PWD/ThirdLibary/android/include
    DEPENDPATH += $$PWD/ThirdLibary/android/include
    DEFINES += ANDROID
} else:win32 {
    INCLUDEPATH += $$PWD/ThirdLibary/windows/include
    DEPENDPATH += $$PWD/ThirdLibary/windows/include
    OPENCV_VERSION=300
} else:unix {
    INCLUDEPATH += $$PWD/ThirdLibary/unix/include
    DEPENDPATH += $$PWD/ThirdLibary/unix/include
}

FFMPEG_LIBRARY= -lavcodec -lavformat -lswscale -lswresample -lpostproc -lavfilter  -lavutil

OPENCV_LIBRARY=-lopencv_core$$OPENCV_VERSION \
    -lopencv_highgui$$OPENCV_VERSION \
    -lopencv_imgproc$$OPENCV_VERSION \
    -lopencv_cudaimgproc$$OPENCV_VERSION

android{
} else {
    OPENCV_LIBRARY += -llibjpeg  -llibwebp -llibtiff -llibpng -llibjasper -lIlmImf -lzlib
}

win32:CONFIG(release, debug|release){
    LIBS += -L$$PWD/ThirdLibary/windows/lib -l$$QXMPP_LIBRARY_NAME $$OPENCV_LIBRARY $$FFMPEG_LIBRARY
} else:win32:CONFIG(debug, debug|release){
    DEFINES += DEBUG
    LIBS += -L$$PWD/ThirdLibary/windows/lib -l$${QXMPP_LIBRARY_NAME}0 $$OPENCV_LIBRARY $$FFMPEG_LIBRARY
} else:android:CONFIG(release, debug|release){
    LIBS += -L$$PWD/ThirdLibary/android/lib -l$$QXMPP_LIBRARY_NAME  $$OPENCV_LIBRARY  $$FFMPEG_LIBRARY
} else:android:CONFIG(debug, debug|release){
    DEFINES += DEBUG
    LIBS += -L$$PWD/ThirdLibary/android/lib -l$$QXMPP_LIBRARY_NAME  $$OPENCV_LIBRARY  $$FFMPEG_LIBRARY
} else:unix: LIBS += -L$$PWD/ThirdLibary/unix/lib -l$$QXMPP_LIBRARY_NAME $$OPENCV_LIBRARY $$FFMPEG_LIBRARY

QXMPP_USE_VPX=1
QXMPP_USE_SPEEX=1

!isEmpty(QXMPP_USE_SPEEX) {
    DEFINES += QXMPP_USE_SPEEX
    LIBS += -lspeex
}

!isEmpty(QXMPP_USE_THEORA) {
    DEFINES += QXMPP_USE_THEORA
    LIBS += -ltheoradec -ltheoraenc
}

!isEmpty(QXMPP_USE_VPX) {
    DEFINES += QXMPP_USE_VPX
    LIBS += -lvpx
    android {
        LIBS+= -lcpu-features
    }
}

DEFINES += __STDC_CONSTANT_MACROS #ffmpegÈúÄË¶?

include(RabbitIm.pri)

CONFIG += mobility

MOBILITY = 

CONFIG += localize_deployment #Êú¨Âú∞ËØ≠Ë®ÄÈÉ®ÁΩ≤

TRANSLATIONS += \
    Resource/translations/app_zh_CN.ts

OTHER_FILES += README.md \
    .gitignore \
    Resource/translations/app_zh_CN.ts \
    docs/* \
    docs/Books/* \
    docs/QXmppÈü≥ËßÜÈ¢ëÂëºÂè´ÊµÅÁ®?.txt \
    ThirdLibary/build/build_android_ffmpeg.sh \
    ThirdLibary/build/build_android_speex.sh \
    ThirdLibary/build/build_andriod_speexdsp.sh \
    ThirdLibary/build/build_android_x264.sh \
    ThirdLibary/build/build_windows_libvpx.sh \
    ThirdLibary/build/build_windows_ffmpeg.sh \
    ThirdLibary/build/build_windows_speex.sh \
    ThirdLibary/build/build_windows_speexdsp.sh \
    ThirdLibary/build/build_android_speexdsp.sh \
    ThirdLibary/build/build_windows_x264.sh


ANDROID_EXTRA_LIBS = 
